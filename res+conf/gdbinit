set disassembly-flavor intel
set history save
set history filename ~/.gdb_history
set history size unlimited
set debug-file-directory /usr/lib/debug

set listsize 30

define list-start
    py gdb.execute("x/{}i $pc".format(gdb.parameter("listsize")))
end
document list-start
List instructions starting from the program counter ($pc).
end
alias ls = list-start

define list-instructions
    py gdb.execute("x/{}i $pc - 0x30".format(gdb.parameter("listsize")))
end
document list-instructions
List instructions around program counter ($pc).
end
alias li = list-instructions
define l
    list-instructions
end
document list-instructions
List instructions around program counter ($pc).
end

define li-display
    disable display
    py gdb.execute("display/{}i {:#x}".format( \
        gdb.parameter("listsize"), gdb.selected_frame().pc()))
end
document li-display
Set the current program counter ($pc) for display.
end

define nextc
    py arch = gdb.selected_frame().architecture()
    py pc = lambda: int(gdb.selected_frame().pc())
    py inst = lambda: arch.disassemble(pc())[0]["asm"].split()
    py isstop = lambda inst: inst[0] in ("call", "ret")
    py gdb.execute("nexti", to_string=True)
    py while not isstop(inst()): gdb.execute("ni", to_string=True)
    list-instructions
    py iscall = inst()[0] == "call"
    py if iscall: print("Callee:\n")
    py if iscall: gdb.execute("x/i $" + inst()[1])
end
document nextc
Continue until next call or return.
end
alias nc = nextc
